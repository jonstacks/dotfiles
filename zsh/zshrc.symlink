# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

if command -v flox >/dev/null 2>&1; then
  eval "$(flox activate -d "$ZSH" -m run)"
else
  echo "[Warning] Flox is not installed. Packages from default environment will not be available."
fi

if command -v sheldon >/dev/null 2>&1; then
  export ZSH="$HOME/.local/share/sheldon/repos/github.com/ohmyzsh/ohmyzsh"
  export SHELDON_CONFIG_FILE="$HOME/.dotfiles/zsh/sheldon/plugins.toml"
  eval "$(sheldon source)"
else
  echo "[Warning] Sheldon is not installed. Zsh plugins will not be loaded."
fi

function prompt_flox_envs() {
  if [[ -v FLOX_PROMPT_ENVIRONMENTS ]]; then
    p10k segment -s WARM -f yellow -t "FLOX_ENVS=[${FLOX_PROMPT_ENVIRONMENTS}]"
  fi
}

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ${ZDOTDIR:-$HOME}/.p10k.zsh ]] || source ${ZDOTDIR:-$HOME}/.p10k.zsh

POWERLEVEL9K_LEFT_PROMPT_ELEMENTS+=(flox_envs)
